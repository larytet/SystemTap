# syscall. compat_rt_sigprocmask _______________________________________________

@define _SYSCALL_COMPAT_RT_SIGPROCMASK_NAME
%(
	name = "rt_sigprocmask"
%)

@define _SYSCALL_COMPAT_RT_SIGPROCMASK_ARGSTR
%(
	argstr = sprintf("%s, %s, %p, %d", how_str, set_str, oldset_uaddr, sigsetsize)
%)

probe syscall.compat_rt_sigprocmask = dw_syscall.compat_rt_sigprocmask !, nd_syscall.compat_rt_sigprocmask {}
probe syscall.compat_rt_sigprocmask.return = dw_syscall.compat_rt_sigprocmask.return !, nd_syscall.compat_rt_sigprocmask.return {}

# dw_compat_rt_sigprocmask _____________________________________________________

probe dw_syscall.compat_rt_sigprocmask =
	kernel.function("compat_sys_rt_sigprocmask").call ?,
	kernel.function("sys32_rt_sigprocmask").call ?
{
	@_SYSCALL_COMPAT_RT_SIGPROCMASK_NAME
	how = __int32($how)
	how_str = _sigprocmask_how_str(how)
	set_uaddr = @__pointer(@choose_defined($set, $nset))
	set_str = _stp_compat_sigset_u(set_uaddr)
	oldset_uaddr = __uint32($oset)
	sigsetsize = __uint32($sigsetsize)
	@_SYSCALL_COMPAT_RT_SIGPROCMASK_ARGSTR
}
probe dw_syscall.compat_rt_sigprocmask.return =
	kernel.function("compat_sys_rt_sigprocmask").return ?,
	kernel.function("sys32_rt_sigprocmask").return ?
{
	@_SYSCALL_COMPAT_RT_SIGPROCMASK_NAME
	retstr = return_str(1, $return)
}

# nd_compat_rt_sigprocmask _____________________________________________________

probe nd_syscall.compat_rt_sigprocmask =
	kprobe.function("compat_sys_rt_sigprocmask") ?,
	kprobe.function("sys32_rt_sigprocmask") ?
{
	@_SYSCALL_COMPAT_RT_SIGPROCMASK_NAME
	if (ppfunc() != "compat_sys_rt_sigprocmask")
		asmlinkage()
	how = int_arg(1)
	how_str = _sigprocmask_how_str(how)
	set_uaddr = pointer_arg(2)
	set_str = _stp_compat_sigset_u(set_uaddr)
	oldset_uaddr = pointer_arg(3)
	sigsetsize = uint_arg(4)
	@_SYSCALL_COMPAT_RT_SIGPROCMASK_ARGSTR
}
probe nd_syscall.compat_rt_sigprocmask.return =
	kprobe.function("compat_sys_rt_sigprocmask").return ?,
	kprobe.function("sys32_rt_sigprocmask").return ?
{
	@_SYSCALL_COMPAT_RT_SIGPROCMASK_NAME
	retstr = returnstr(1)
}
